// SPDX-FileCopyrightText: Copyright (c) 2026 NVIDIA CORPORATION & AFFILIATES. All rights reserved.
// SPDX-License-Identifier: MIT

#pragma once

#include <cuda_tile.h>
#include <cuda_fp16.h>
#include <cuda_bf16.h>
#include <cuda_fp8.h>

constexpr float INV_LOG_2 = 1.442695040888963f;  // 1/ln(2)

namespace ct = cuda::tiles;

template <typename T>
__tile__ inline auto zero_tile() {
    return ct::zeros<ct::tile<T, ct::shape<1>>>();
}

/**
 * MLA Decoding: QK = Q @ K^T + QPE @ KPE^T, then softmax and V matmul
 * Inputs: Q [B, num_head, D], QPE [B, num_head, KPE], KV [B, S_kv, D], KPE_in [B, S_kv, KPE]
 *
 * Template Parameters:
 *   T: Element type (float, __half, __nv_bfloat16)
 *   BLOCK_D: Hidden dimension block size
 *   BLOCK_H: Head block size
 *   BLOCK_N: Sequence block size
 *   BLOCK_KPE: Position embedding dimension
 */
template<typename T, int BLOCK_D, int BLOCK_H, int BLOCK_N, int BLOCK_KPE>
[[ using cutile : hint(0,num_cta_in_cga=1, occupancy=1) ]]
__tile_global__ void naive_absorb_mla(
    T* __restrict__ Q, T* __restrict__ QPE,
    T* __restrict__ KV, T* __restrict__ KPE_in,
    T* __restrict__ Out, float* __restrict__ L,
    float sm_scale,
    long long stride_qb, int stride_qm, long long stride_qpeb, int stride_qpem,
    long long stride_kvb, int stride_kvn, long long stride_kpeb, int stride_kpem,
    long long stride_ob, int stride_om, int B, int num_head, int S_kv) {

    Q      = ct::assume_aligned<16>(Q);
    QPE    = ct::assume_aligned<16>(QPE);
    KV     = ct::assume_aligned<16>(KV);
    KPE_in = ct::assume_aligned<16>(KPE_in);
    Out    = ct::assume_aligned<16>(Out);
    L      = ct::assume_aligned<16>(L);

    // Tile type definitions - use float for accumulation
    using f32_HxD = ct::tile<float, ct::shape<BLOCK_H, BLOCK_D>>;
    using f32_HxKPE = ct::tile<float, ct::shape<BLOCK_H, BLOCK_KPE>>;
    using f32_NxD = ct::tile<float, ct::shape<BLOCK_N, BLOCK_D>>;
    using f32_NxKPE = ct::tile<float, ct::shape<BLOCK_N, BLOCK_KPE>>;
    using f32_DxN = ct::tile<float, ct::shape<BLOCK_D, BLOCK_N>>;
    using f32_KPExN = ct::tile<float, ct::shape<BLOCK_KPE, BLOCK_N>>;
    using f32_HxN = ct::tile<float, ct::shape<BLOCK_H, BLOCK_N>>;
    using f32_H = ct::tile<float, ct::shape<BLOCK_H>>;
    using TxHxD = ct::tile<T, ct::shape<BLOCK_H, BLOCK_D>>;
    using TxHxKPE = ct::tile<T, ct::shape<BLOCK_H, BLOCK_KPE>>;
    using TxNxD = ct::tile<T, ct::shape<BLOCK_N, BLOCK_D>>;
    using TxNxKPE = ct::tile<T, ct::shape<BLOCK_N, BLOCK_KPE>>;
    using i32_H = ct::tile<int, ct::shape<BLOCK_H>>;
    using i32_N = ct::tile<int, ct::shape<BLOCK_N>>;
    using i32_D = ct::tile<int, ct::shape<BLOCK_D>>;
    using i32_KPE = ct::tile<int, ct::shape<BLOCK_KPE>>;

    int pid_x = ct::bid().x;
    int batch_idx = ct::bid().y;
    float qk_scale = sm_scale * INV_LOG_2;

    // Base pointers
    T* Q_base = Q + batch_idx * stride_qb + pid_x * BLOCK_H * stride_qm;
    T* QPE_base = QPE + batch_idx * stride_qpeb + pid_x * BLOCK_H * stride_qpem;
    T* KV_base = KV + batch_idx * stride_kvb;
    T* KPE_base = KPE_in + batch_idx * stride_kpeb;

    // Generate 1D offset tiles
    auto offs_h = ct::iota<i32_H>();
    auto offs_d = ct::iota<i32_D>();
    auto offs_kpe = ct::iota<i32_KPE>();
    auto offs_n = ct::iota<i32_N>();

    // Reshape to 2D for broadcasting
    auto offs_h_2d = ct::reshape(offs_h, ct::shape<BLOCK_H, 1>{});
    auto offs_d_2d = ct::reshape(offs_d, ct::shape<1, BLOCK_D>{});
    auto offs_kpe_2d = ct::reshape(offs_kpe, ct::shape<1, BLOCK_KPE>{});

    // Load Q: [BLOCK_H, BLOCK_D]
    auto q_flat_offs = offs_h_2d * stride_qm + offs_d_2d;
    auto q_ptrs = Q_base + q_flat_offs;
    auto q_mask = ct::reshape(offs_h + ct::full<i32_H>(pid_x * BLOCK_H) < num_head, ct::shape<BLOCK_H, 1>{});
    auto q = ct::load_masked(q_ptrs, q_mask, zero_tile<T>());

    // Load QPE: [BLOCK_H, BLOCK_KPE]
    auto qpe_flat_offs = offs_h_2d * stride_qpem + offs_kpe_2d;
    auto qpe_ptrs = QPE_base + qpe_flat_offs;
    auto qpe = ct::load_masked(qpe_ptrs, q_mask, zero_tile<T>());

    // Initialize accumulators
    auto m_i = ct::full<f32_H>(-1e30f);
    auto l_i = ct::full<f32_H>(1.0f);
    auto acc = ct::zeros<f32_HxD>();

    int num_kv_blocks = (S_kv + BLOCK_N - 1) / BLOCK_N;
    for (auto block_idx : ct::irange(0, num_kv_blocks)) {
        int curr_n = block_idx * BLOCK_N;

        auto k_row_base = ct::full<i32_N>(curr_n) + offs_n;
        auto k_row_2d = ct::reshape(k_row_base, ct::shape<BLOCK_N, 1>{});
        auto k_flat_offs = k_row_2d * stride_kvn + offs_d_2d;
        auto k_ptrs = KV_base + k_flat_offs;
        auto k_mask = ct::reshape(k_row_base < S_kv, ct::shape<BLOCK_N, 1>{});
        auto k = ct::load_masked(k_ptrs, k_mask, zero_tile<T>());  // [BLOCK_N, BLOCK_D]

        auto kpe_flat_offs = k_row_2d * stride_kpem + offs_kpe_2d;
        auto kpe_ptrs = KPE_base + kpe_flat_offs;
        auto kpe_block = ct::load_masked(kpe_ptrs, k_mask, zero_tile<T>());  // [BLOCK_N, BLOCK_KPE]

        // Compute QK = Q @ K^T: [BLOCK_H, BLOCK_D] @ [BLOCK_D, BLOCK_N] = [BLOCK_H, BLOCK_N]
        auto k_trans = ct::transpose(k);  // [BLOCK_D, BLOCK_N]
        auto qk = ct::mma(q, k_trans, ct::zeros<f32_HxN>());

        // Add QPE @ KPE^T: [BLOCK_H, BLOCK_KPE] @ [BLOCK_KPE, BLOCK_N]
        auto kpe_trans = ct::transpose(kpe_block);  // [BLOCK_KPE, BLOCK_N]
        qk = ct::mma(qpe, kpe_trans, qk);

        auto n_valid = ct::reshape(k_row_base < S_kv, ct::shape<1, BLOCK_N>{});
        qk = ct::select(n_valid, qk, ct::full<f32_HxN>(-1e6f));

        qk = qk * qk_scale;

        auto m_ij_2d = ct::reduce_max(qk, ct::integral_constant<1>{});
        auto m_ij = ct::reshape(m_ij_2d, ct::shape<BLOCK_H>{});
        m_ij = ct::max(m_i, m_ij);

        auto m_ij_bcast = ct::reshape(m_ij, ct::shape<BLOCK_H, 1>{});
        auto p = ct::exp2(qk - m_ij_bcast);

        auto l_curr_2d = ct::sum(p, ct::integral_constant<1>{});
        auto l_curr = ct::reshape(l_curr_2d, ct::shape<BLOCK_H>{});

        auto alpha = ct::exp2(m_i - m_ij);
        l_i = l_i * alpha + l_curr;

        auto alpha_2d = ct::reshape(alpha, ct::shape<BLOCK_H, 1>{});
        acc = acc * alpha_2d;

        auto v = ct::load_masked(k_ptrs, k_mask, zero_tile<T>());

        acc = ct::mma(ct::element_cast<T>(p), v, acc);

        m_i = m_ij;
    }

    // Finalize: output = acc / l_i
    auto l_i_2d = ct::reshape(l_i, ct::shape<BLOCK_H, 1>{});
    auto output = ct::div(acc, l_i_2d, ct::round_approximate_t{}, ct::round_subnormals_to_zero_t{});

    // Store output: [BLOCK_H, BLOCK_D]
    T* O_base = Out + batch_idx * stride_ob + pid_x * BLOCK_H * stride_om;
    auto o_flat_offs = offs_h_2d * stride_om + offs_d_2d;
    auto o_ptrs = O_base + o_flat_offs;
    ct::store_masked(o_ptrs, ct::element_cast<T>(output), q_mask);

    // Store L = m_i + log2(l_i): [BLOCK_H]
    float* L_base = L + batch_idx * num_head + pid_x * BLOCK_H;
    auto l_result = m_i + ct::log2(l_i);
    auto l_ptrs = L_base + offs_h;
    auto l_mask = offs_h + ct::full<i32_H>(pid_x * BLOCK_H) < num_head;
    ct::store_masked(l_ptrs, l_result, l_mask);
}


/**
 * MLA Decoding Transpose Kernel
 *
 * A transpose variant of the MLA decoding kernel that computes attention
 * in a transposed layout for potentially better cache efficiency.
 *
 * Computes: QK = K @ Q^T + KPE @ QPE^T (in transposed [N, H] order)
 * Then transposes back for final output.
 */
template<typename T, int BLOCK_D, int BLOCK_H, int BLOCK_N, int BLOCK_KPE,
         int EVEN_STRIDES, int S_KV, bool EVEN_N>
__tile_global__ void naive_absorb_mla_transpose(
    T* __restrict__ Q, T* __restrict__ QPE,
    T* __restrict__ KV, T* __restrict__ KPE_in,
    T* __restrict__ Out, float* __restrict__ L,
    float sm_scale,
    long long stride_qb, int stride_qm, long long stride_qpeb, int stride_qpem,
    long long stride_kvb, int stride_kvn, long long stride_kpeb, int stride_kpem,
    long long stride_ob, int stride_om, int B, int num_head) {
    namespace ct = cuda::tiles;

    Q      = ct::assume_aligned<16>(Q);
    QPE    = ct::assume_aligned<16>(QPE);
    KV     = ct::assume_aligned<16>(KV);
    KPE_in = ct::assume_aligned<16>(KPE_in);
    Out    = ct::assume_aligned<16>(Out);
    L      = ct::assume_aligned<16>(L);

    if constexpr (EVEN_STRIDES) {
        using tma_elems_t = ct::integral_constant<static_cast<int>(16 / sizeof(T))>;
        stride_qb = ct::assume_divisible(stride_qb, tma_elems_t{});
        stride_qm = ct::assume_divisible(stride_qm, tma_elems_t{});
        stride_qpeb = ct::assume_divisible(stride_qpeb, tma_elems_t{});
        stride_qpem = ct::assume_divisible(stride_qpem, tma_elems_t{});
        stride_kvb = ct::assume_divisible(stride_kvb, tma_elems_t{});
        stride_kvn = ct::assume_divisible(stride_kvn, tma_elems_t{});
        stride_kpeb = ct::assume_divisible(stride_kpeb, tma_elems_t{});
        stride_kpem = ct::assume_divisible(stride_kpem, tma_elems_t{});
        stride_ob = ct::assume_divisible(stride_ob, tma_elems_t{});
        stride_om = ct::assume_divisible(stride_om, tma_elems_t{});
        num_head = ct::assume_divisible(num_head, ct::integral_constant<16>{});
    }

    // Tile type definitions - use float for accumulation
    using f32_HxD = ct::tile<float, ct::shape<BLOCK_H, BLOCK_D>>;
    using f32_DxH = ct::tile<float, ct::shape<BLOCK_D, BLOCK_H>>;
    using f32_HxKPE = ct::tile<float, ct::shape<BLOCK_H, BLOCK_KPE>>;
    using f32_KPExH = ct::tile<float, ct::shape<BLOCK_KPE, BLOCK_H>>;
    using f32_NxD = ct::tile<float, ct::shape<BLOCK_N, BLOCK_D>>;
    using f32_DxN = ct::tile<float, ct::shape<BLOCK_D, BLOCK_N>>;
    using f32_NxKPE = ct::tile<float, ct::shape<BLOCK_N, BLOCK_KPE>>;
    using f32_NxH = ct::tile<float, ct::shape<BLOCK_N, BLOCK_H>>;
    using f32_H = ct::tile<float, ct::shape<BLOCK_H>>;
    using TxHxD = ct::tile<T, ct::shape<BLOCK_H, BLOCK_D>>;
    using TxHxKPE = ct::tile<T, ct::shape<BLOCK_H, BLOCK_KPE>>;
    using TxNxD = ct::tile<T, ct::shape<BLOCK_N, BLOCK_D>>;
    using TxNxKPE = ct::tile<T, ct::shape<BLOCK_N, BLOCK_KPE>>;
    using i32_H = ct::tile<int, ct::shape<BLOCK_H>>;
    using i32_N = ct::tile<int, ct::shape<BLOCK_N>>;
    using i32_D = ct::tile<int, ct::shape<BLOCK_D>>;
    using i32_KPE = ct::tile<int, ct::shape<BLOCK_KPE>>;

    int pid_x = ct::bid().x;
    int batch_idx = ct::bid().y;
    float qk_scale = sm_scale * INV_LOG_2;

    auto q_view = ct::partition_view{
        ct::tensor_span{Q,
                        ct::layout_strided_mapping{
                            ct::extents{B, ct::integral_constant<BLOCK_D>{}, num_head},
                            ct::extents{stride_qb, ct::integral_constant<1>{}, stride_qm}}},
        ct::shape<1, BLOCK_D, BLOCK_H>{}};
    auto qpe_view = ct::partition_view{
        ct::tensor_span{QPE,
                        ct::layout_strided_mapping{
                            ct::extents{B, ct::integral_constant<BLOCK_KPE>{}, num_head},
                            ct::extents{stride_qpeb, ct::integral_constant<1>{}, stride_qpem}}},
        ct::shape<1, BLOCK_KPE, BLOCK_H>{}};
    auto kv_view = ct::partition_view{
        ct::tensor_span{KV,
                        ct::layout_strided_mapping{
                            ct::extents{B, ct::integral_constant<S_KV>{}, ct::integral_constant<BLOCK_D>{}},
                            ct::extents{stride_kvb, stride_kvn, ct::integral_constant<1>{}}}},
        ct::shape<1, BLOCK_N, BLOCK_D>{}};
    auto kpe_view = ct::partition_view{
        ct::tensor_span{KPE_in,
                        ct::layout_strided_mapping{
                            ct::extents{B, ct::integral_constant<S_KV>{}, ct::integral_constant<BLOCK_KPE>{}},
                            ct::extents{stride_kpeb, stride_kpem, ct::integral_constant<1>{}}}},
        ct::shape<1, BLOCK_N, BLOCK_KPE>{}};

    auto offs_h = ct::iota<i32_H>();
    auto offs_n = ct::iota<i32_N>();

    typename decltype(q_view)::view_tile_type q_3d;
    [[ using cutile : hint(0, allow_tma=true) ]]
    q_3d = q_view.load_masked(batch_idx, 0, pid_x);
    auto q = ct::reshape(q_3d, ct::shape<BLOCK_D, BLOCK_H>{});

    typename decltype(qpe_view)::view_tile_type qpe_3d;
    [[ using cutile : hint(0, allow_tma=true) ]]
    qpe_3d = qpe_view.load_masked(batch_idx, 0, pid_x);
    auto qpe = ct::reshape(qpe_3d, ct::shape<BLOCK_KPE, BLOCK_H>{});

    // Initialize accumulators in transposed layout [BLOCK_D, BLOCK_H]
    auto m_i = ct::full<f32_H>(-1e30f);
    auto l_i = ct::full<f32_NxH>(1.0f);  // [BLOCK_N, BLOCK_H] for intermediate sum
    auto acc = ct::zeros<f32_DxH>();  // [BLOCK_D, BLOCK_H]

    // Loop over K/V blocks — use ct::irange to keep `m_i`, `l_i`, `acc`
    // accumulator tiles in shared memory (avoids local-memory spills).
    constexpr int num_kv_blocks = (S_KV + BLOCK_N - 1) / BLOCK_N;
    for (auto block_idx : ct::irange(0, num_kv_blocks)) {
        int curr_n = block_idx * BLOCK_N;

        auto k_row_base = ct::full<i32_N>(curr_n) + offs_n;
        typename decltype(kv_view)::view_tile_type k_3d;
        [[ using cutile : hint(0, allow_tma=true, latency=2) ]]
        k_3d = kv_view.load_masked(batch_idx, block_idx, 0);
        auto k = ct::reshape(k_3d, ct::shape<BLOCK_N, BLOCK_D>{});
        typename decltype(kpe_view)::view_tile_type kpe_3d;
        [[ using cutile : hint(0, allow_tma=true, latency=2) ]]
        kpe_3d = kpe_view.load_masked(batch_idx, block_idx, 0);
        auto kpe_block = ct::reshape(kpe_3d, ct::shape<BLOCK_N, BLOCK_KPE>{});

        // Compute QK = K @ Q^T: [BLOCK_N, BLOCK_D] @ [BLOCK_D, BLOCK_H] = [BLOCK_N, BLOCK_H]
        auto qk = ct::mma(k, q, ct::zeros<f32_NxH>());  // [BLOCK_N, BLOCK_H]

        // Add KPE @ QPE^T: [BLOCK_N, BLOCK_KPE] @ [BLOCK_KPE, BLOCK_H]
        qk = ct::mma(kpe_block, qpe, qk);

        if constexpr (!EVEN_N) {
            constexpr int mask_start = (S_KV / BLOCK_N) * BLOCK_N;
            if (curr_n >= mask_start) {
                auto n_valid = ct::reshape(k_row_base < S_KV, ct::shape<BLOCK_N, 1>{});
                qk = ct::select(n_valid, qk, ct::full<f32_NxH>(-1e6f));
            }
        }

        // Compute column-wise max (dim 0): [BLOCK_H] - max over N dimension
        auto m_ij_2d = ct::reduce_max(qk, ct::integral_constant<0>{});  // [1, BLOCK_H]
        auto m_ij = ct::reshape(m_ij_2d, ct::shape<BLOCK_H>{});
        m_ij = m_ij * qk_scale;
        m_ij = ct::max(m_i, m_ij);

        // Compute exp2(qk * qk_scale - m_ij): [BLOCK_N, BLOCK_H]
        auto m_ij_bcast = ct::reshape(m_ij, ct::shape<1, BLOCK_H>{});
        auto p = ct::exp2(qk * qk_scale - m_ij_bcast);

        // Update running statistics
        auto alpha = ct::exp2(m_i - m_ij);
        auto alpha_bcast = ct::reshape(alpha, ct::shape<1, BLOCK_H>{});
        l_i = l_i * alpha_bcast + p;  // [BLOCK_N, BLOCK_H]

        // Scale accumulator [BLOCK_D, BLOCK_H]
        acc = acc * alpha_bcast;

        // Load V: [BLOCK_N, BLOCK_D] and transpose to [BLOCK_D, BLOCK_N]
        typename decltype(kv_view)::view_tile_type v_3d;
        [[ using cutile : hint(0, allow_tma=true, latency=2) ]]
        v_3d = kv_view.load_masked(batch_idx, block_idx, 0);
        auto v = ct::reshape(v_3d, ct::shape<BLOCK_N, BLOCK_D>{});
        auto v_trans = ct::transpose(v);  // [BLOCK_D, BLOCK_N]

        acc = ct::mma(v_trans, ct::element_cast<T>(p), acc);

        m_i = m_ij;
    }

    // Sum l_i over N dimension: [BLOCK_N, BLOCK_H] -> [BLOCK_H]
    auto l_sum_2d = ct::sum(l_i, ct::integral_constant<0>{});  // [1, BLOCK_H]
    auto l_sum = ct::reshape(l_sum_2d, ct::shape<BLOCK_H>{});

    // Finalize: output = acc / l_sum, transposed back to [BLOCK_H, BLOCK_D]
    auto l_sum_bcast = ct::reshape(l_sum, ct::shape<1, BLOCK_H>{});
    acc = ct::div(acc, l_sum_bcast, ct::round_approximate_t{},
                  ct::round_subnormals_to_zero_t{});  // [BLOCK_D, BLOCK_H]
    auto output = ct::transpose(ct::element_cast<T>(acc));  // [BLOCK_H, BLOCK_D]

    // Store output: [BLOCK_H, BLOCK_D]
    auto o_view = ct::partition_view{
        ct::tensor_span{Out,
                        ct::layout_strided_mapping{
                            ct::extents{B, num_head, ct::integral_constant<BLOCK_D>{}},
                            ct::extents{stride_ob, stride_om, ct::integral_constant<1>{}}}},
        ct::shape<1, BLOCK_H, BLOCK_D>{}};
    auto output_3d = ct::reshape(output, ct::shape<1, BLOCK_H, BLOCK_D>{});
    [[ using cutile : hint(0, allow_tma=true) ]]
    o_view.store_masked(output_3d, batch_idx, pid_x, 0);

    // Store L = m_i + log2(l_sum): [BLOCK_H]
    auto l_view = ct::partition_view{
        ct::tensor_span{L, ct::extents{B, num_head}},
        ct::shape<1, BLOCK_H>{}};
    auto l_result = ct::reshape(m_i + ct::log2(l_sum), ct::shape<1, BLOCK_H>{});
    [[ using cutile : hint(0, allow_tma=true) ]]
    l_view.store_masked(l_result, batch_idx, pid_x);
}
